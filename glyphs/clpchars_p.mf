%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%                                                                         %
% MF Calport v1.1                                                         %
% © 2017 by Marco Mueller                                                 %
% thanks to Linus Roemer, Alexis Reigel, Jeremy Gibbons                   %
% <http://www.metaflop.com>                                               %
% licensed under gpl v3: www.gnu.org/licenses/                            %
%                                                                         %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%  

% numbers

clpchar("zero",5u#,ht#,0);
pickup cornerpen;
x1=0.9w;
x2=x4=0.5w;
x3=0.1w;
top y2=h+o;
y1=y3=0.5h;
bot y4=-o;
interdotdraw z4{right}..z1{up}..z2{left}..z3{down}..cycle;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("one",3.5u#,ht#,0);
pickup cornerpen;
x1=x2=0.7w;
x3=x5=0.1w;
bot y1=0;
top y2=h;
y3=0.7h;
dotdraw z1--z2--z3;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("two",5u#,ht#,0);
pickup cornerpen;
x2=x3=0.125w;
x5=x6=0.5w;
x4=x7=0.875w;
bot y3=bot y4=0;
top y6=h+o;
y5=0.5h;
y2=y7=0.7h+0.5o;
interdotdraw z2{up}..z6{right}..z7{down}..z3{down};
dotdraw z3--z4;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("three",5u#,ht#,0);
pickup cornerpen;
x1=0.075w;
x3=0.1w;
x4=x5=0.5w;
x6=x8=0.875w;
bot y4=0-o;
top y3=top y6=h;
y5=0.6h;
y1=y8=0.3h+0.25px-0.5o;
dotdraw z3--z6--z5;
interdotdraw z5{right}..z8{down}..z4{left}..z1{up};
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("four",4.5u#,ht#,0);
pickup cornerpen;
x1=0.1w;
x6=0.3w;
x2=x3=x4=0.7w;
x5=w;
bot y3=0;
top y2=top y6=h;
y1=y4=y5=0.4h;
dotdraw z2--z4--z3;
dotdraw z6--z1--z4--z5;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("five",5u#,ht#,0);
pickup cornerpen;
x1=0.125w;
x3=x7=0.175w;
x5=x4=0.5w;
x8=0.875w;
x6=0.825w;
bot y4=0-o;
top y3=top y6=h;
y5=0.65h;
y7=0.55h;
y1=y8=0.325h+0.25px-0.5o;
dotdraw z6--z3--z7;
interdotdraw z7..z5{right}..z8{down}..z4{left}..z1{up};
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("six",5u#,ht#,0);
pickup cornerpen;
x1=x7=0.125w;
x3=x5=x4=0.5w;
x8=0.875w;
x6=0.825w;
bot y4=0-o;
top y3=h+o;
y6=0.85h;
y5=0.65h;
y7=0.55h;
y1=y8=0.325h+0.25px-0.5o;
interdotdraw z1{up}..z5{right}..z8{down}..z4{left}..cycle;
interdotdraw z1{up}--z7{up}..z3{right}..z6;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("seven",4.5u#,ht#,0);
pickup cornerpen;
x1=0.1w;
x2=0.9w;
x3=0.25w;
bot y3=0;
top y1=top y2=h;
dotdraw z3--z2--z1;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("eight",4.5u#,ht#,0);
pickup cornerpen;
x1=0.125w;
x7=x1+0.5px;
x3=x5=x4=0.5w;
x8=0.875w;
x6=x8-0.5px;
bot y4=0-o;
y1=y8=0.275h;
top y3=h+o;
y6=y7=0.75h;
y5=0.525h;
interdotdraw z5{left}..z1{down}..z4{right}..z8{up}..cycle;
interdotdraw z5{right}..z6{up}..z3{left}..z7{down}..cycle;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("nine",5u#,ht#,0);
pickup cornerpen;
x1=x7=0.875w;
x3=x5=x4=0.5w;
x8=0.125w;
x6=0.175w;
top y4=h+o;
bot y3=0-o;
y6=0.15h;
y5=0.35h;
y7=0.45h;
y1=y8=0.675h-0.25px+0.5o;
interdotdraw z6..z3{right}..z7{up}--z1{up};
interdotdraw z1{up}..z4{left}..z8{down}..z5{right}..cycle;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

% punctation

clpchar("visiblespace",5u#,x_ht#,desc_depth#);
pickup cornerpen;
x1=x2=w-0.5px;
x3=x4=0+0.5px;
y1=y3=0;
y2=y4=-0.5d;
dotdraw z1--z2--z4--z3;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("period",1.5u#,x_ht#,0);
pickup cornerpen;
x1=0.5w;
bot y1=0;
dotdraw z1;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("colon",1.5u#,x_ht#,0);
pickup cornerpen;
x1=x2=0.5w;
bot y1=0;
top y2=h;
dotdraw z1;
dotdraw z2;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("comma",1.5u#,x_ht#,desc_depth#);
pickup cornerpen;
x1=0.5w;
x2=0.15w;
bot y1=0;
y2=-0.75d;
dotdraw z1--z2;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("semicolon",1.5u#,x_ht#,desc_depth#);
pickup cornerpen;
x1=x3=0.5w;
x2=0.15w;
bot y1=0;
y2=-0.75d;
top y3=h;
dotdraw z1--z2;
dotdraw z3;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("exclam",2u#,ht#,desc_depth#);
pickup cornerpen;
x1=x5=x6=0.5w;
bot y1=0;
y5=0.3h;
top y6=h;
dotdraw z1;
dotdraw z5--z6;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("question",5u#,ht#,desc_depth#);
pickup cornerpen;
x1=x5=x6=x8=0.5w;
x7=0.9w;
x9=0.1w;
bot y1=0;
y5=0.3h;
y6=0.45h;
y7=y9=0.725h-0.25px+0.5o;
top y8=h+o;
dotdraw z1;
interdotdraw z5--z6{right}..z7{up}..z8{left}..z9{down};
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("hyphen",5u#,x_ht#,0);
pickup cornerpen;
x1=0.1w;
x2=0.9w;
y1=y2=0.7h;
dotdraw z1--z2;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("emdash",5u#,x_ht#,0);
pickup cornerpen;
x1=0;
x2=w;
y1=y2=0.7h;
dotdraw z1--z2;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("endash",5u#,x_ht#,0);
pickup cornerpen;
x1=0.1w;
x2=0.9w;
y1=y2=0.7h;
dotdraw z1--z2;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("cwm",5u#,x_ht#,0);
endchar;

clpchar("ampersand",5u#,ht#,0);
pickup cornerpen;
x1=0;
x7=0.2w;
x5=0.35w;
x4=0.5w;
x3=0.5w;
x8=w;
x9=w-0.5px;
x6=0.8w;
bot y4=0-o;
bot y9=0;
top y3=h+o;
y6=y7=0.8h+0.5o;
y5=0.6h;
y1=y8=0.3h+0.5o;
dy=angle(z9-z5);
%z5=whatever[z1,z2]=whatever[z4,z6];
dotdraw z9--z5{-dir dy}..z7{up}..z3{right}..z6{down}..z5..z1{down}..z4{right}..z8;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

% upper case

clpchar("A",5u#,ht#,0);
pickup cornerpen;
x1=0.05w;
x2=0.5w;
x3=0.95w;
bot y1=bot y3=0;
top y2=h+o;
y4=y5=0.4h;
z4=whatever[z1,z2];
z5=whatever[z2,z3];
dotdraw z1--z4--z2--z5--z3;
dotdraw z4--z5;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("B",5u#,ht#,0);
pickup cornerpen;
x1=x2=x3=0.15w;
x4=x5=x6=0.6w;
x7=x8=0.9w;
bot y1=bot y4=0;
top y3=top y6=h;
y2=y5=0.5h;
y7=0.75h;
y8=0.25h;
dotdraw z1--z2--z3;
interdotdraw z3--z6{right}..z7{down}..z5{left}--z2;
interdotdraw z2--z5{right}..z8{down}..z4{left}--z1;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("C",6u#,ht#,0);
pickup cornerpen;
x1=x5=0.9w;
x2=x4=0.5w;
x3=0.1w;
top y2=h+o;
y1=0.725h;
y3=0.5h;
y5=0.275h;
bot y4=-o;
interdotdraw z1..z2{left}..z3{down}..z4{right}..z5;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("D",5u#,ht#,0);
pickup cornerpen;
x1=x2=0.15w;
x3=x4=0.5w;
x5=0.95w;
bot y1=bot y3=0;
top y2=top y4=h;
y5=0.5h;
dotdraw z1--z2;
interdotdraw z2--z4{right}..z5{down}..z3{left}--z1;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("E",4.25u#,ht#,0);
pickup cornerpen;
x1=x2=x5=0.2w;
x3=x4=0.9w;
x6=0.9w;
bot y1=bot y3=0;
top y2=top y4=h;
y5=y6=0.5h;
dotdraw z3--z1--z5--z2--z4;
dotdraw z5--z6;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("F",4.25u#,ht#,0);
pickup cornerpen;
x1=x2=x5=0.2w;
x4=0.9w;
x6=0.9w;
bot y1=0;
top y2=top y4=h;
y5=y6=0.5h;
dotdraw z1--z5--z2--z4;
dotdraw z5--z6;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("G",6u#,ht#,0);
pickup cornerpen;
x1=x6=0.9w;
x2=x4=0.5w;
x7=0.5w;
x3=0.1w;
top y2=h+o;
y1=0.725h;
y3=0.5h;
y6=y7=0.4h;
bot y4=-o;
interdotdraw z1..z2{left}..z3{down}..z4{right}..z6;
dotdraw z6--z7;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("H",5u#,ht#,0);
pickup cornerpen;
x1=x2=x5=0.15w;
x3=x4=x6=0.85w;
bot y1=bot y3=0;
top y2=top y4=h;
y5=y6=0.5h;
dotdraw z1--z5--z2;
dotdraw z3--z6--z4;
dotdraw z5--z6;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("I",2.5u#,ht#,0);
pickup cornerpen;
x1=x2=0.5w;
bot y1=0;
top y2=h;
dotdraw z1--z2;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("J",4u#,ht#,0);
pickup cornerpen;
x2=x5=0;
x4=0.425w;
x1=x3=0.85w;
top y1=top y2=h;
y3=0.35h;
y5=0.3h;
bot y4=-o;
dotdraw z1--z3{down}..z4{left}..z5;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("K",5u#,ht#,0);
pickup cornerpen;
x1=x2=x4=0.15w;
x3=0.95w;
x5=0.85w;
x6=0.2w;
y4=0.5h;
bot y2=bot y3=0;
top y1=top y5=top y6=h;
z7=whatever[z3,z6]=whatever[z4,z5];
dotdraw z3--z7;
dotdraw z4--z7--z5;
dotdraw z1--z4--z2;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("L",4.5u#,ht#,0);
pickup cornerpen;
x1=x2=0.2w;
x3=0.9w;
bot y2=bot y3=0;
top y1=h;
dotdraw z1--z2--z3;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("M",6.5u#,ht#,0);
pickup cornerpen;
x1=x2=0.15w;
x3=x4=0.85w;
x5=0.5w;
bot y1=bot y3=0;
top y2=top y4=h;
y5=0.5h;
dotdraw z1--z2--z5--z4--z3;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("N",5u#,ht#,0);
pickup cornerpen;
x1=x2=0.15w;
x3=x4=0.85w;
bot y1=bot y3=0;
top y2=top y4=h;
dotdraw z1--z2--z3--z4;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("O",6u#,ht#,0);
pickup cornerpen;
x1=0.9w;
x2=x4=0.5w;
x3=0.1w;
top y2=h+o;
y1=y3=0.5h;
bot y4=-o;
interdotdraw z4{right}..z1{up}..z2{left}..z3{down}..cycle;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("P",5u#,ht#,0);
pickup cornerpen;
x1=x2=x3=0.15w;
x5=x6=0.55w;
x7=0.9w;
bot y1=0;
top y3=top y6=h;
y2=y5=0.5h;
y7=0.75h-0.25px;
dotdraw z1--z2--z3;
interdotdraw z3--z6{right}..z7{down}..z5{left}--z2;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("Q",6u#,ht#,desc_depth#);
pickup cornerpen;
x1=x6=0.9w;
x2=x4=0.5w;
x3=0.1w;
top y2=h+o;
y1=y3=0.5h;
y5=0.25h;
y6=-0.5desc_depth;
bot y4=-o;
interdotdraw z4{right}..z1{up}..z2{left}..z3{down}..cycle;
dotdraw z4--z6;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("R",5.5u#,ht#,0);
pickup cornerpen;
x1=x2=x3=0.15w;
x5=x6=0.55w;
x7=0.825w;
x4=w-0.5px;
bot y1=bot y4=0;
top y3=top y6=h;
y2=y5=0.5h;
y7=0.75h-0.25px;
dotdraw z1--z2--z3;
interdotdraw z3--z6{right}..z7{down}..z5{left};
dotdraw z2--z5--z4;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("S",5u#,ht#,0);
pickup cornerpen;
x1=x2=0.1w;
x3=x5=x6=0.5w;
x4=x7=0.9w;
bot y3=0-o;
top y6=h+o;
y1=y4=0.25h+0.25px;
y5=0.5h;
y2=y7=0.75h-0.25px;
interdotdraw z1{down}..z3{right}..z4{up}..z2{up}..z6{right}..z7{down};
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("T",5u#,ht#,0);
pickup cornerpen;
x1=x2=0.5w;
x3=0.05w;
x4=0.95w;
bot y1=0;
top y2=top y3=top y4=h;
dotdraw z1--z2;
dotdraw z3--z2--z4;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("U",6u#,ht#,0);
pickup cornerpen;
x1=x2=0.15w;
x3=0.5w;
x4=x7=0.85w;
bot y3=-o;
y1=y4=0.275h;
top y2=top y7=h;
interdotdraw z2--z1{down}..z3{right}..z4{up}--z7;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("V",5u#,ht#,0);
pickup cornerpen;
x1=0.05w;
x3=0.5w;
x4=0.95w;
bot y3=0;
top y1=top y4=h;
dotdraw z1--z3--z4;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("W",6.75u#,ht#,0);
pickup cornerpen;
x2=0.1w;
x1=0.3w;
x5=0.5w;
x3=0.7w;
x4=0.9w;
bot y1=bot y3=0;
top y2=top y4=h;
y5=0.5h;
dotdraw z2--z1--z5--z3--z4;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("X",4.5u#,ht#,0);
pickup cornerpen;
x1=0.05w;
x2=0.1w;
x3=0.95w;
x4=0.9w;
bot y1=bot y3=0;
top y2=top y4=h;
z5=whatever[z2,z3]=whatever[z1,z4];
dotdraw z1--z5--z4;
dotdraw z2--z5--z3;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("Y",5u#,ht#,0);
pickup cornerpen;
x2=0.1w;
x4=0.9w;
x1=x5=0.5w;
bot y1=0;
top y2=top y4=h;
y5=0.5h;
dotdraw z2--z5--z4;
dotdraw z1--z5;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("Z",4.5u#,ht#,0);
pickup cornerpen;
x1=x2=0.1w;
x3=x4=0.9w;
x5=0.5w;
bot y1=bot y3=0;
top y2=top y4=h;
y5=0.5h;
dotdraw z2--z4--z1--z3;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

% lower case

clpchar("a",4.5u#,x_ht#,0);
pickup cornerpen;
x1=x2=x4=0.85w;
x3=x5=0.475w;
x6=0.1w;
bot y1=0;
top y2=h;
top y3=h+o;
bot y5=-o;
y4=y6=0.5h;
dotdraw z1--z4--z2;
interdotdraw z4{up}..z3{left}..z6{down}..z5{right}..cycle;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("b",4.5u#,ht#,0);
pickup cornerpen;
x4=0.9w;
x3=x5=0.525w;
x1=x2=x6=0.15w;
bot y1=0;
y2=h;
top y3=x_ht+o;
bot y5=-o;
y4=y6=0.5x_ht+0.25px;
dotdraw z1--z6--z2;
interdotdraw z6{down}..z5{right}..z4{up}..z3{left}..cycle;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("c",4.5u#,x_ht#,0);
pickup cornerpen;
x1=x5=0.8w;
x2=x4=0.5w;
x3=0.1w;
top y2=h+o;
y1=0.75h;
y3=0.5h;
y5=0.25h;
bot y4=-o;
interdotdraw z1..z2{left}..z3{down}..z4{right}..z5;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("d",4.5u#,ht#,0);
pickup cornerpen;
x1=x2=x4=0.85w;
x3=x5=0.475w;
x6=0.1w;
bot y1=0;
y2=h;
y3=x_ht+o;
bot y5=-o;
y4=y6=0.5x_ht+0.25px;
dotdraw z1--z4--z2;
interdotdraw z4{up}..z3{left}..z6{down}..z5{right}..cycle;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("e",4.5u#,x_ht#,0);
pickup cornerpen;
x1=0.9w;
x2=x4=0.5w;
x5=0.8w;
x3=0.1w;
top y2=h+o;
y1=y3=0.5h;
y5=0.2h;
bot y4=-o;
dotdraw z3--z1..z2{left}..z3{down}..z4{right}..z5;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("f",3.5u#,ht#,0);
pickup cornerpen;
x5=0.1w;
x1=x2=x7=0.5w;
x4=x6=0.9w;
y4=h+o;
bot y1=0;
y2=y5=y6=x_ht-0.5px;
y7=x_ht+0.4(h-x_ht);
dotdraw z1--z2;
interdotdraw z2--z7{up}..z4{right};
dotdraw z5--z2--z6;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("g",4.5u#,x_ht#,desc_depth#);
pickup cornerpen;
x1=x2=x4=0.9w;
x3=x5=x7=0.5w;
x6=x8=0.1w;
y1=-0.25desc_depth;
top y2=h;
top y3=h+o;
bot y5=-o;
y4=y6=0.5x_ht;
y7=-desc_depth-o;
y8=-0.5desc_depth;
dotdraw z2--z4;
interdotdraw z4--z1{down}..z7{left}..z8;
interdotdraw z4{up}..z3{left}..z6{down}..z5{right}..cycle;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("h",4.5u#,ht#,0);
pickup cornerpen;
x4=x5=0.9w;
x3=0.5w;
x1=x2=x6=0.15w;
bot y1=0;
y2=h;
y3=x_ht+o;
bot y5=0;
y4=y6=0.6x_ht;
dotdraw z1--z6--z2;
interdotdraw z5--z4{up}..z3{left}..z6{down};
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("i",1.5u#,ht#,0);
pickup cornerpen;
x1=x2=x4=0.5w;
bot y1=0;
y2=x_ht;
top y4=ht;
dotdraw z1--z2;
dotdraw z4;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("j",3u#,ht#,desc_depth#);
pickup cornerpen;
x1=x2=x4=0.8w;
x7=0.1w;
y1=-0.2d;
top y2=x_ht;
top y4=ht;
y7=-desc_depth-o;
interdotdraw z2--z1{down}..z7{left};
dotdraw z4;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("k",4u#,ht#,0);
pickup cornerpen;
x3=x5=0.95w;
x1=x2=x4=x6=0.15w;
bot y1=0;
y2=h;
y4=0.9h;
y3=x_ht;
bot y5=0;
y6=0.5x_ht;
z7=whatever[z3,z6]=whatever[z4,z5];
dotdraw z1--z6--z2;
dotdraw z6--z7--z3;
dotdraw z7--z5;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("l",1.5u#,ht#,0);
pickup cornerpen;
x1=x2=0.5w;
bot y1=0;
top y2=h;
dotdraw z1--z2;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("m",7u#,x_ht#,0);
pickup cornerpen;
x4=x5=0.5w;
x3=0.3w;
x7=0.7w;
x8=x9=0.9w;
x1=x2=x6=0.1w;
bot y1=bot y5=bot y9=0;
top y2=h;
top y3=top y7=h+o;
y4=y6=y8=0.6h;
dotdraw z1--z6--z2;
dotdraw z5--z4{up}..z3{left}..z6{down};
dotdraw z9--z8{up}..z7{left}..z4{down};
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("n",4.5u#,x_ht#,0);
pickup cornerpen;
x4=x5=0.85w;
x3=0.5w;
x1=x2=x6=0.15w;
bot y1=bot y5=0;
top y2=h;
top y3=h+o;
y4=y6=0.5h;
dotdraw z1--z6--z2;
interdotdraw z5--z4{up}..z3{left}..z6{down};
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("o",4.5u#,x_ht#,0);
pickup cornerpen;
x1=0.9w;
x2=x4=0.5w;
x3=0.1w;
top y2=h+o;
y1=y3=0.5h;
bot y4=-o;
interdotdraw z2{left}..z3{down}..z4{right}..z1{up}..cycle;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("p",4.5u#,x_ht#,desc_depth#);
pickup cornerpen;
x4=0.9w;
x3=x5=0.525w;
x1=x2=x6=0.15w;
y1=-desc_depth;;
top y2=h;
top y3=h+o;
bot y5=-o;
y4=y6=0.5x_ht;
dotdraw z1--z6--z2;
interdotdraw z6{down}..z5{right}..z4{up}..z3{left}..cycle;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("q",4.5u#,x_ht#,desc_depth#);
pickup cornerpen;
x1=x2=x4=0.85w;
x3=x5=0.475w;
x6=0.1w;
y1=-desc_depth;;
top y2=h;
top y3=h+o;
bot y5=0-o;
y4=y6=0.5x_ht;
dotdraw z1--z4--z2;
interdotdraw z4{up}..z3{left}..z6{down}..z5{right}..cycle;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("r",4u#,x_ht#,0);
pickup cornerpen;
x3=0.525w;
x5=0.9w;
x1=x2=x6=0.15w;
bot y1=0;
top y2=top y3=h;
y5=0.75h;
y6=0.5h;
dotdraw z1--z6--z2;
interdotdraw z5..z3{left}..z6{down};
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("s",4u#,x_ht#,0);
pickup cornerpen;
x1=x2=0.1w;
x3=x5=x6=0.5w;
x4=x7=0.9w;
bot y3=0-o;
top y6=h+o;
y1=y4=0.25h+0.25px;
y5=0.5h;
y2=y7=0.75h-0.25px;
interdotdraw z1{down}..z3{right}..z4{up}..z5{left}..z2{up}..z6{right}..z7{down};
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("t",3.5u#,ht#,0);
pickup cornerpen;
x2=x3=x1=0.5w;
x5=0.1w;
x6=0.9w;
y3=h;
bot y1=0;
y2=y5=y6=x_ht-0.5px;
dotdraw z1--z2--z3;
dotdraw z5--z2--z6;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("u",4.5u#,x_ht#,0);
pickup cornerpen;
x1=x2=0.1w;
x3=0.5w;
x4=x5=x7=0.9w;
bot y3=-o;
bot y5=0;
y1=y4=0.4h;
top y2=top y7=h;
interdotdraw z2--z1{down}..z3{right}..z4{up};
dotdraw z4--z5--z7;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("v",4.5u#,x_ht#,0);
pickup cornerpen;
x1=0.1w;
x3=0.5w;
x4=0.9w;
bot y3=0;
top y1=top y4=h;
dotdraw z1--z3--z4;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("w",5u#,x_ht#,0);
pickup cornerpen;
x2=0;
x1=0.25w;
x5=0.5w;
x3=0.75w;
x4=w;
bot y1=bot y3=0;
top y2=top y4=h;
y5=0.8h;
dotdraw z2--z1--z5--z3--z4;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("x",4.5u#,x_ht#,0);
pickup cornerpen;
x1=0.1w;
x2=0.125w;
x3=0.9w;
x4=0.875w;
bot y1=bot y3=0;
top y2=top y4=h;
z5=whatever[z2,z3]=whatever[z1,z4];
dotdraw z1--z5--z4;
dotdraw z2--z5--z3;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("y",4.5u#,x_ht#,desc_depth#);
pickup cornerpen;
x2=x6=0.9w;
x1=0.45w;
x3=x4=x7=0.1w;
y1=-0.6d;
top y2=top y3=h;
y7=-d-o;
y4=y6=0;
dz=angle(z2-z1);
z5=whatever[z1,z2]=whatever[z4,z6];
dotdraw z2--z5;
interdotdraw z5--z1{-dir dz}..z7{left};
dotdraw z3--z5;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;

clpchar("z",4u#,x_ht#,0);
pickup cornerpen;
x1=0.1w;
x2=0.125w;
x3=0.9w;
x4=0.875w;
bot y1=bot y3=0;
top y2=top y4=h;
dotdraw z2--z4--z1--z3;
adjustwidth(xgap#,xgap#);
penlabels(range 1 thru 150);
endchar;